grammar org.archstudio.prolog.xtext.Prolog hidden(WHITESPACE, SINGLE_LINE_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate prolog "http://www.archstudio.org/prolog/xtext/Prolog"

Program:
	(clauses+=Clause)* (query=Query)?;

Clause:
	(predicates+=ExpressionINF) (':-' predicates+=ExpressionINF (',' predicates+=ExpressionINF)*)? '.';

Query:
	'?-' predicates+=ExpressionINF (',' predicates+=ExpressionINF)* '.';

ExpressionINF returns Expression:
	Expression1100;

Expression1100 returns Expression:
	Expression900 (ops+=EXPRESSION_1100 exps+=Expression900)*;

terminal EXPRESSION_1100:
	';';

Expression900 returns Expression:
	(ops+=EXPRESSION_900)? exps+=Expression700;

terminal EXPRESSION_900:
	'\\+' | 'not';

Expression700 returns Expression:
	Expression500 (ops+=EXPRESSION_700 exps+=Expression500)?;

terminal EXPRESSION_700:
	'<' | '=' | '=..' | '=@=' | '=:=' | '=<' | '==' | '=\\=' | '>' | '>=' | '@<' | '@=<' | '@>' | '@>=' | '\\=' | '\\=='
	| 'is';

Expression500 returns Expression:
	Expression400 (ops+=EXPRESSION_500 exps+=Expression400)*;

terminal EXPRESSION_500:
	'+' | '-' | '/\\' | '\\/' | 'xor';

Expression400 returns Expression:
	Expression200 (ops+=EXPRESSION_400 exps+=Expression200)*;

terminal EXPRESSION_400:
	'*' | '/' | '//' | 'rdiv' | '<<' | '>>' | 'mod' | 'rem';

Expression200 returns Expression:
	Expression0 (ops+=EXPRESSION_200 exps+=Expression0)*;

terminal EXPRESSION_200:
	'**' | '^';

Expression0 returns Expression:
	(ops+=(ATOM | EXPRESSION_1100 | EXPRESSION_900 | EXPRESSION_700 | EXPRESSION_500 | EXPRESSION_400 | EXPRESSION_200)
	(complex?='(' (exps+=ExpressionINF (',' exps+=ExpressionINF)*)? ')')?)
	| (number=NUMBER)
	| (string=STRING)
	| (variable=VARIABLE)
	| (list?='[' (head+=ExpressionINF (',' head+=ExpressionINF)* ('|' tail=ExpressionINF)?)? ']')
	| (list?='.' '(' head+=ExpressionINF ',' tail=ExpressionINF ')')
	| '(' exps+=ExpressionINF ')';

terminal NUMBER:
	'-'? ('0'..'9')+;

terminal ATOM:
	('a'..'z') ('0'..'9' | 'a'..'z' | 'A'..'Z' | '_')*;

terminal VARIABLE:
	('A'..'Z' | '_') ('0'..'9' | 'a'..'z' | 'A'..'Z' | '_')*;

terminal STRING:
	'\'' ('0'..'9' | 'a'..'z' | 'A'..'Z' | '_' | '+' | '-' | '*' | '/' | '\\' | '^' | '~' | ':' | '.' | '?' |
	'@' | '#' | '$' | '&' | ' ')* '\'';

terminal WHITESPACE:
	(' ' | '\t' | '\r' | '\n')+;

terminal SINGLE_LINE_COMMENT:
	'%' !('\n' | '\r')* ('\r'? '\n')?;
